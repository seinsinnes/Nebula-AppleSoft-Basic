10 REM PROMPT USER FOR ANGLE
30 PRINT "I WILL DRAW YOU A TREE"
40 PRINT "ENTER AN ANGLE IN RADIANS"
50 INPUT DA

90 REM INITIAL VARIABLES
100 LET SHRINK = 0.67
110 LET A = PI * 0.5
120 LET L = 50
130 LET LEVEL = 1
140 LET MAXLEV = 10
150 LET X = 125 : LET Y = 0

160 REM ARRAY FOR STACK OF TREE POSITIONS
170 DIM S(MAXLEV) : DIM T(MAXLEV)

180 REM START BRANCHING
190 GO SUB 1000

1000 REM DRAW A SINGLE BRANCH!

1010 REM CALCULATE NEXT POINT
1020 LET DX = L * COS(A)
1030 LET DY = L * SIN(A)

1040 REM DRAW BRANCH
1050 PLOT X, Y
1060 DRAW DX, DY

1070 REM SAVE POSITION
1080 S(LEVEL) = X : T(LEVEL) = Y

1090 REM NEXT BRANCH
1100 LET X = X + DX : LET Y = Y + DY
1110 LET LEVEL = LEVEL + 1
1120 LET A = A + DA
1130 LET L = L * SHRINK
1140 IF LEVEL < MAXLEV THEN GO SUB 1000

1150 REM AND ANOTHER BRANCH
1160 LET A = A - DA * 2
1170 IF LEVEL < MAXLEV THEN GO SUB 1000

1180 REM POP BACK
1190 LET A = A + DA
1200 LET L = L / SHRINK
1210 LET LEVEL = LEVEL - 1
1220 LET X = S(LEVEL) : LET Y = T(LEVEL)
1230 RETURN
