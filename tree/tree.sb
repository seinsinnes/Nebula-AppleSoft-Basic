   10 REM Prompt user for angle
   20 PRINT "I will draw you tree."
   30 PRINT "Enter an angle in radians"
   40 INPUT DA
   90 REM Initial variables
  100 LET SHRINK=0.67
  110 LET A = PI*0.5
  120 LET L=50
  130 LET LEVEL=1
  140 LET MAXLEV=10
  150 LET X=125: LET Y=0

  160 REM Array for stack of tree positions
  170 DIM S(MAXLEV): DIM T(MAXLEV)

  180 REM Start branching
  190 GO SUB 1000
  200 STOP

 1000 REM Draw a single branch

 1010 REM Calculate next point
 1020 LET DX = L*COS (A)
 1030 LET DY = L*SIN (A)

 1040 REM Draw branch
 1050 PLOT X,Y
 1060 DRAW DX,DY

 1070 REM Save position
 1080 LET S(LEVEL)=X: LET T(LEVEL)=Y

 1090 REM Next branch
 1100 LET X=X+DX: LET Y=Y+DY
 1110 LET LEVEL = LEVEL + 1
 1120 LET A=A+DA
 1130 LET L = L*SHRINK
 1140 IF LEVEL < MAXLEV THEN GO SUB 1000

 1150 REM And another branch
 1160 LET A=A-DA*2
 1170 IF LEVEL < MAXLEV THEN GO SUB 1000

 1180 REM Pop back
 1190 LET A=A+DA
 1200 LET L=L/SHRINK
 1210 LET LEVEL=LEVEL-1
 1220 LET X=S(LEVEL): LET Y=T(LEVEL)
 1230 RETURN
